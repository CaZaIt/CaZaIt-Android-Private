plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-parcelize'
}

android {
    namespace 'org.cazait'
    compileSdk 33

    defaultConfig {
        applicationId "org.cazait"
        minSdk 28
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        dataBinding true
    }
}

configurations {
    ktlint
    detekt
}

dependencies {
    // ktlint
    ktlint("com.pinterest:ktlint:0.48.2") {
        attributes {
            attribute(Bundling.BUNDLING_ATTRIBUTE, getObjects().named(Bundling, Bundling.EXTERNAL))
        }
    }

    // detekt
    detekt ("io.gitlab.arturbosch.detekt:detekt-cli:1.22.0-RC3")

    implementation "androidx.core:core-ktx:$coreKtxVersion"
    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "com.google.android.material:material:$materialVersion"

    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'

    // DataBinding
    implementation "androidx.databinding:databinding-runtime:$databindingVersion"

    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$viewModelVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$viewModelVersion"

    // Hilt Library
    implementation ("com.google.dagger:hilt-android:$hiltVersion")
    kapt ("com.google.dagger:hilt-android-compiler:$hiltVersion")
    kapt ("androidx.hilt:hilt-compiler:$hiltCompilerVersion")

    // Glide Library
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    // Gson
    implementation ("com.squareup.retrofit2:converter-gson:$gsonVersion")

    // okHttp
    implementation ("com.squareup.okhttp3:logging-interceptor:$okHttpVersion")
    implementation ("com.squareup.okhttp3:okhttp:$okHttpVersion")

    // splash
    implementation("androidx.core:core-splashscreen:$splashVersion")

    // coil
    implementation("io.coil-kt:coil:2.2.2")

    // paging3
    implementation("androidx.activity:activity-ktx:1.7.0")
    implementation("androidx.paging:paging-runtime-ktx:3.1.1")

    // Dot Indicator
    implementation 'com.tbuonomo:dotsindicator:4.3'

    // 네이버 지도 SDK
    implementation("com.naver.maps:map-sdk:$naverMapVersion")
    implementation("com.google.android.gms:play-services-location:$playServicesLocationVersion")

    // EasyPermissions
    implementation("pub.devrel:easypermissions:$easyPermissionVersion")

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    mainClass.set("com.pinterest.ktlint.Main")
    args "src/**/*.kt"
    // see https://pinterest.github.io/ktlint/install/cli/#command-line-usage for more information
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    mainClass.set("com.pinterest.ktlint.Main")
    args "-F", "src/**/*.kt"
    // see https://pinterest.github.io/ktlint/install/cli/#command-line-usage for more information
}

task detekt(type: JavaExec) {
    mainClass.set("io.gitlab.arturbosch.detekt.cli.Main")
    classpath = configurations.detekt

    def input = "$projectDir"
    def exclude = ".*/build/.*,.*/resources/.*"
    def config = "$rootDir/detekt.yml"
    def params = [ '-i', input, '-ex', exclude, '-c', config]

    args(params)
}